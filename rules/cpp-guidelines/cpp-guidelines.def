
. basic.def

def es76
	# https://isocpp.github.io/CppCoreGuidelines/CppCoreGuidelines#es76-avoid-goto
	r;      # reset the marks
	m goto; # mark all goto statements
	>1;     # store current marks
	m for;  # mark all for statements
	n {;    # go to next open bracket 
	s };    # strech the window to the matching closing bracket
	c for;  # see if there is a nested loop in the window
	n for;  # move the mark to the next for
	n {;    # go to the next open bracket 
	s };    # stretch window to the matching closing bracket
	c goto; # retain the marks that contain a goto (in the nested loop)
	n goto; # move the mark to that goto
	> 2;    # store the marks (all goto statements in a nested loop
	<1;     # load all the goto marks
	<^2;    # remove all marks from 1 that are in nested loops
	json es76-avoid-goto cpp\core\guidelines\-\avoid\goto;
end

def r10
	# https://isocpp.github.io/CppCoreGuidelines/CppCoreGuidelines#Rr-mallocfree
	r; 
	m /alloc;
	m free;
	json r10-avoid-malloc-free cpp\core\guidelines\-\avoid\malloc\and\free;
end

def es50
	# https://isocpp.github.io/CppCoreGuidelines/CppCoreGuidelines#es50-dont-cast-away-const
	r;
	m const_cast;
	json es50-avoid-const_cast cpp\core\guidelines\-\dont\cast\away\const;
end

def es49
	# https://isocpp.github.io/CppCoreGuidelines/CppCoreGuidelines#es49-if-you-must-use-a-cast-use-a-named-cast
	r;
	m reinterpret_cast;
	json es49-avoid-reinterpret_cast cpp\core\guidelines\-\avoid\use\of\reinterpret_cast;
end


def i2
	# https://isocpp.github.io/CppCoreGuidelines/CppCoreGuidelines#i2-avoid-non-const-global-variables
	r;
	m @type;
	w (.curly==0 && .round==0);
	>1;
	e @ident =;
	> 2;
	<1;
	e @ident \;;
	<|2;
	>1;
	m const;
	n;
	s @ident;
	w (.curly==0 );
	>2; <1; <^2; >1;
	m static;
	w (.curly==0 );
	n;
	s @ident;
	>2; <1; <^2;
	> 1;
	m static;
	w (.curly==0);
	n;
	n;
	s @ident;
	>2; <1; <^2;
	> 1;
	m const;
	w (.curly==0);
	n;
	n;
	s @ident;
	>2; <1; <^2;
	> 1;
	m extern;
	w (.curly==0 );
	n;
	n;
	s @ident;
	>2; <1; <^2;
	> 1;
	m extern;
	w (.curly==0 );
	n;
	s @ident;
	>2; <1; <^2;
	json i2-avoid-non-const-global-variables cpp\core\guidelines\-\avoid\non-const\global\variables;
end
